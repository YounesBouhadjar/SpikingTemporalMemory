
synapse stdsp_homeostasis_synapse:

  state:
    w real = 1.  @nest::weight
    p real = 1.
    t_last_pre_spike ms = -1ms

  parameters:
    d ms = 2.0 ms  @nest::delay # !!! cannot have a variable called "delay"
    lambda real = .01
    lambda_minus real = .01
    tau_tr_pre ms = 20 ms
    tau_tr_post ms = 20 ms
    mu_plus real = 1
    Wmax real = 100.
    Pmax real = 20.
    Pmin real = 0.
    th_perm real = 10.
    zt real = 1. pA
    dt_min ms = - 5. ms
    dt_max ms = - 50. ms

  equations:
    kernel pre_trace_kernel = exp(-t / tau_tr_pre)
    inline pre_trace real = convolve(pre_trace_kernel, pre_spikes)

    # all-to-all trace of postsynaptic neuron
    kernel post_trace_kernel = exp(-t / tau_tr_post)
    inline post_trace real = convolve(post_trace_kernel, post_spikes)

  input:
    pre_spikes nS <- spike
    post_spikes nS <- spike
    z_post pA <- continuous

  output:
    spike

  onReceive(post_spikes):
    delta_t ms = t_last_pre_spike - ( t + d )
    # potentiate synapse
    p_ real = 0.
    if delta_t > dt_max and delta_t < dt_min:
       p_ = p + Pmax * lambda * ( zt - z_post ) * ( p / Pmax )**mu_plus * pre_trace
       p = min(Pmax, p_)
    elif delta_t > dt_min:
       p_ = p + Pmax * lambda * ( zt - z_post ) * ( p / Pmax )**mu_plus * ( pre_trace - exp( delta_t / tau_tr_pre) )
       p = min(Pmax, p_)

  onReceive(pre_spikes):
    t_last_pre_spike = t
    # depress synapse
    p_ real = p - lambda_minus * Pmax
    p = max(Pmin, p_)

    if p > th_perm:
      w = Wmax
    else:
      w = 0.

    # deliver spike to postsynaptic partner
    deliver_spike(w, d)

